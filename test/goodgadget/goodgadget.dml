# 
# goodgadget.dml
#
#
#    AS1---AS2
#     \ AS0 /
#      \   /
#       AS3
#
# ASn = the AS whose NHI prefix is n
# 
#
# The three points of the triangle (AS1, AS2, and AS3) all hate the direct
# path to AS0.  AS1 and AS2 do not accept counter-clockwise paths and prefer
# the clockwise paths.  AS3 has no preference between clockwise and counter-
# clockwise paths.
#
# There is one solution: ([3 0], [2 3 0], [1 2 3 0]).
#
#
# Thanks to Tim Griffin for contributing this test network. Refer to the
# SIGCOMM '99 paper "An Analysis of BGP Convergence Properties" (Griffin
# and Wilfong) for similar examples of "gadgets" like this one, including
# some which are not so "good" (they don't converge).
#
#

_schema [ _find .schemas.Net ]

Net [ # the all-encompassing Net

  frequency 1000000000    # nanosecond simulation resolution

  bgpoptions [
    validation_test 12
    startup_jitter_bound 0.1
  ]

  Net [
    id 0
    AS_status boundary

    router [
      id 1
      graph [
        ProtocolSession [
          name bgp  use SSF.OS.BGP4.BGPSession
          autoconfig false
          connretry_time 120  min_as_orig_time 15
          reflector false
          neighbor [ as 1  address 1(1)  use_return_address 1(1)
                     _extends .basic_ebgp_neighbor ]
          neighbor [ as 2  address 1(1)  use_return_address 1(2)
                     _extends .basic_ebgp_neighbor ]
          neighbor [ as 3  address 1(1)  use_return_address 1(3)
                     _extends .basic_ebgp_neighbor ]
        ]
        ProtocolSession [ name socket use SSF.OS.Socket.socketMaster ]
        ProtocolSession [ name tcp    use SSF.OS.TCP.tcpSessionMaster  warn false ]
        ProtocolSession [ name ip     use SSF.OS.IP ]
      ]
      interface [ id 0  virtual true ]
      interface [ idrange [ from 1 to 3 ] ]
    ]
  ]

  Net [
    id 1
    AS_status boundary
    router [
      id 1
      graph [
        ProtocolSession [
          name bgp  use SSF.OS.BGP4.BGPSession 
          autoconfig false
          connretry_time 120  min_as_orig_time 15
          reflector false
          neighbor [
            as 0  address 1(1)  use_return_address 1(1)
            hold_time 90  keep_alive_time 30  mrai 30
            infilter  [ # give low priority to routes learned from 0
              clause [
                precedence 1
                predicate []
                action [
                  primary permit
                  atom [ attribute local_pref  type set  value 80 ] 
                ] 
              ] 
            ]
            outfilter [ _extends .filters.permit_all ]
          ]
          neighbor [
            as 2  address 1(2)  use_return_address 1(2)
            hold_time 90  keep_alive_time 30  mrai 30
            infilter [ # give high priority to routes learned from 2
              clause [
                precedence 1
                predicate []
                action [
                  primary permit 
                  atom [ attribute local_pref  type set  value 100 ] 
                ] 
              ] 
            ]
            outfilter [ _extends .filters.permit_all ]
          ]
          neighbor [
            as 3  address 1(2)  use_return_address 1(3)
            hold_time 90  keep_alive_time 30  mrai 30
            infilter [ # deny all routes learned from 3
              clause [ precedence 1  predicate []  action [ primary deny ] ] 
            ]
            outfilter [ _extends .filters.permit_all ]
          ]
        ]
        ProtocolSession [ name socket use SSF.OS.Socket.socketMaster ]
        ProtocolSession [ name tcp    use SSF.OS.TCP.tcpSessionMaster  warn false ]
        ProtocolSession [ name ip     use SSF.OS.IP ]
      ]
      interface [ id 0  virtual true ]
      interface [ idrange [ from 1 to 3 ] ]
    ]
  ]

  Net [
    id 2
    AS_status boundary
    router [
      id 1
      graph [
        ProtocolSession [
          name bgp  use SSF.OS.BGP4.BGPSession 
          autoconfig false
          connretry_time 120  min_as_orig_time 15
          reflector false
          neighbor [
            as 0  address 1(2)  use_return_address 1(1)
            hold_time 90  keep_alive_time 30  mrai 30
            infilter  [ # give low priority to routes learned from 0
              clause [
                precedence 1
                predicate []
                action [
                  primary permit
                  atom [ attribute local_pref  type set  value 80 ] 
                ] 
              ] 
            ]
            outfilter [ _extends .filters.permit_all ]
          ]
          neighbor [
            as 1  address 1(2)  use_return_address 1(2)
            hold_time 90  keep_alive_time 30  mrai 30
            infilter [ # deny all routes learned from 1
              clause [ precedence 1  predicate []  action [ primary deny ] ] 
            ]
            outfilter [ _extends .filters.permit_all ]
          ]
          neighbor [
            as 3  address 1(3)  use_return_address 1(3)
            hold_time 90  keep_alive_time 30  mrai 30
            infilter [ # give high priority to routes learned from 3
              clause [
                precedence 1
                predicate []
                action [
                  primary permit 
                  atom [ attribute local_pref  type set  value 100 ] 
                ] 
              ] 
            ]
            outfilter [ _extends .filters.permit_all ]
          ]
        ]
        ProtocolSession [ name socket use SSF.OS.Socket.socketMaster ]
        ProtocolSession [ name tcp    use SSF.OS.TCP.tcpSessionMaster  warn false ]
        ProtocolSession [ name ip     use SSF.OS.IP ]
      ]
      interface [ id 0  virtual true ]
      interface [ idrange [ from 1 to 3 ] ]
    ]
  ]

  Net [
    id 3
    AS_status boundary
    router [
      id 1
      graph [
        ProtocolSession [
          name bgp  use SSF.OS.BGP4.BGPSession 
          autoconfig false
          connretry_time 120  min_as_orig_time 15
          reflector false
          neighbor [ as 0  address 1(3)  use_return_address 1(1)
                     _extends .basic_ebgp_neighbor ]
          neighbor [ as 1  address 1(3)  use_return_address 1(2)
                     _extends .basic_ebgp_neighbor ]
          neighbor [ as 2  address 1(3)  use_return_address 1(3)
                     _extends .basic_ebgp_neighbor ]
        ]
        ProtocolSession [ name socket use SSF.OS.Socket.socketMaster ]
        ProtocolSession [ name tcp    use SSF.OS.TCP.tcpSessionMaster  warn false ]
        ProtocolSession [ name ip     use SSF.OS.IP ]
      ]
      interface [ id 0  virtual true ]
      interface [ idrange [ from 1 to 3 ] ]
    ]
  ]

  link [ attach 0:1(1) attach 1:1(1) delay 0.01 ]
  link [ attach 0:1(2) attach 2:1(1) delay 0.01 ]
  link [ attach 0:1(3) attach 3:1(1) delay 0.01 ]
  link [ attach 1:1(2) attach 2:1(2) delay 0.01 ]
  link [ attach 1:1(3) attach 3:1(2) delay 0.01 ]
  link [ attach 2:1(3) attach 3:1(3) delay 0.01 ]

] # end all-encompassing Net
